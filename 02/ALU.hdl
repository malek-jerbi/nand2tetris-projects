// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    Mux16(a=x,b=false,sel=zx,out=x1);
    Not16(in=x1,out=xnot);
    Mux16(a=x1,b=xnot,sel=nx,out=x2);

    Mux16(a=y,b=false,sel=zy,out=y1);
    Not16(in=y1,out=ynot);
    Mux16(a=y1,b=ynot,sel=ny,out=y2);

    Add16(a=x2,b=y2,out=sum);
    And16(a=x2,b=y2,out=and);

    Mux16(a=and,b=sum,sel=f,out=result);
    Not16(in=result, out=notresult);

    Mux16(a=result, b=notresult, sel=no, out[0..7]=outLSB, out[8..14]=outMSB, out[15]=MSbit);
    Mux16(a[0..7]=outLSB, a[8..14]=outMSB, a[15]=MSbit, b=true, sel=false, out=out);

    Or8Way(in=outLSB, out=or1);
    Or8Way(in[0..6]=outMSB, in[7]=MSbit, out=or2);
    Or(a=or1,b=or2, out=zrbefore);
    Not(in=zrbefore, out=zr);
    
    Mux(a=MSbit, b=false, out=ng);

}